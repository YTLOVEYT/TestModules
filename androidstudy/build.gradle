if (rootProject.ext.isAndroidStudyModule) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //Arouter路由配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
        //多Dex配置
        multiDexEnabled true
        //Module资源前缀
        resourcePrefix "study_"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (rootProject.ext.isAndroidStudyModule) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude '*module'
                }
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //greendao配置
    greendao {
        //版本号，升级时可配置
        schemaVersion 1
        //自定义生成数据库文件的目录
        targetGenDir 'src/main/java'
        //通过gradle插件生成的数据库相关文件的包名，默认为你的entity所在的包名；
        daoPackage 'com.example.modules.android.study.dao'
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    annotationProcessor rootProject.ext.dependencies["arouter_compiler"]
    annotationProcessor rootProject.ext.dependencies["butterknife_compiler"]

    implementation project(':component_base')
    //刷新
    implementation rootProject.ext.dependencies["SmartRefreshLayout"]
    implementation rootProject.ext.dependencies["SmartRefreshHeader"]
    //rxJava
    implementation rootProject.ext.dependencies["rxJava2"]
    implementation rootProject.ext.dependencies["rxAndroid2"]
    //banner
    implementation rootProject.ext.dependencies["banner"]
    //retrofit
    implementation rootProject.ext.dependencies["retrofit2"]
    implementation rootProject.ext.dependencies["adapter-rxjava2"]
    implementation rootProject.ext.dependencies["converter-gson"]
    implementation rootProject.ext.dependencies["logging-interceptor"]
    //滑动tabLayout
    implementation rootProject.ext.dependencies["FlycoTabLayout_Lib"]

    implementation rootProject.ext.dependencies["greendao3"]


}
